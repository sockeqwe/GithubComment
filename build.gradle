buildscript {
    ext.kotlin_version = '1.2.10'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2'
    }
}

group 'com.hannnesdorfmann'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'com.github.johnrengelman.shadow'


sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/markusamshove/maven" }
}

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}


shadowJar {
    baseName = 'githubcommenter'
    classifier = null
    version = null

    manifest {
        attributes 'Main-Class': 'com.hannesdorfmann.githubcomment.MainKt'
    }
}


dependencies {
    def okhttp = "3.9.1"
    def moshi = "1.5.0"
    def retrofit = "2.3.0"
    def spek = "1.1.5"
    def tikxml = "0.8.13"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit"
    implementation 'commons-cli:commons-cli:1.4'
    implementation "com.squareup.moshi:moshi:$moshi"
    implementation "com.squareup.moshi:moshi-kotlin:$moshi"
    implementation "com.squareup.okhttp3:okhttp:$okhttp"
    implementation "io.reactivex.rxjava2:rxjava:2.1.8"
    implementation 'com.github.stkent:githubdiffparser:1.0.1'
    implementation "com.tickaroo.tikxml:annotation:$tikxml"
    implementation "com.tickaroo.tikxml:core:$tikxml"
    kapt "com.tickaroo.tikxml:processor:$tikxml"

    testImplementation "org.jetbrains.spek:spek-api:$spek"
    testRuntime "org.jetbrains.spek:spek-junit-platform-engine:$spek"
    testImplementation 'org.amshove.kluent:kluent:1.33'
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp"

}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}